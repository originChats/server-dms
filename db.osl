import "osl/fs"

def userExists(string id) bool (
    auto path = "db/users/" ++ id ++ ".json"
    return fs.Exists(path)
)

def userExistsByUsername(string username) bool (
    return getIdByUsername(username) != ""
)

def getIdByUsername(string username) string (
    string path = "db/users/"
    array files = fs.ReadDir(path)
    int i = 1
    while i <= files.len (
        string fname = files[i].toStr()
        if fname.endsWith(".json") (
            string id = fname.trim(1, -6)
            object user = getUser(id)
            if user != null and user.username.toStr().toLower() == username.toLower() (
                return id
            )
        )
        i = i + 1
    )
    return ""
)

def getUsernameById(string id) string (
    object user = getUser(id)
    if user == null (
        return ""
    )
    return user.username.toStr()
)

def getUser(string id) object (
    auto path = "db/users/" ++ id ++ ".json"
    if !fs.Exists(path) (
        object newObject = {
            id: id,
            username: "",
            channels: []
        }
        fs.WriteFile(path, newObject.JsonStringify())
        return newObject
    )

    auto content = fs.ReadFile(path).JsonParse()
    if typeof(content) != "object" (
        return {
            id: id,
            username: "",
            channels: []
        }
    )
    return content.assert(object)
)

def saveUser(string id, object user) (
    auto path = "db/users/" ++ id ++ ".json"
    fs.WriteFile(path, user.JsonStringify())
)

def addChannelToUser(string id, string channelId) (
    object user = getUser(id)

    if typeof(user.channels) != "array" (
        user.channels = []
    )

    array channels = user.channels.assert(array)
    if !channels.contains(channelId) (
        channels.append(channelId)
        user.channels = channels
        saveUser(id, user)
    )
)

def removeChannelFromUser(string id, string channelId) (
    object user = getUser(id)

    if typeof(user.channels) != "array" (
        return
    )

    array channels = user.channels.assert(array)
    array newChannels = []
    int len = channels.len
    int i = 1
    while i <= len (
        string c = channels[i].toStr()
        if c != channelId (
            newChannels.append(c)
        )
        i = i + 1
    )
    user.channels = newChannels
    saveUser(id, user)
)

def getChannel(string channelId) object (
    auto path = "db/channels/" ++ channelId ++ ".json"
    if !fs.Exists(path) (
        return null
    )

    auto content = fs.ReadFile(path).JsonParse()
    if typeof(content) != "object" (
        return null
    )
    return content.assert(object)
)

def saveChannel(string channelId, object channel) (
    auto path = "db/channels/" ++ channelId ++ ".json"
    fs.WriteFile(path, channel.JsonStringify())
)

def randomString(int length) string (
    string chars = "abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789"
    string result = ""
    for i length (
        result = result ++ chars[rand.Intn(chars.len) + 1].toStr()
    )
    return result
)

def findExistingDMChannel(string user1, string user2) string (
    object u1 = getUser(user1)
    if typeof(u1.channels) != "array" (
        return ""
    )

    array channels = u1.channels.assert(array)
    int len = channels.len
    int i = 1
    while i <= len (
        string channelId = channels[i].toStr()
        object ch = getChannel(channelId)
        if ch.type.toStr() == "dm" (
            if typeof(ch.members) == "array" (
                array members = ch.members.assert(array)
                if members.contains(user1) and members.contains(user2) (
                    return channelId
                )
            )
        )
        i += 1
    )
    return ""
)

def createDMChannel(string user1, string user2) string (
    string existingChannel = findExistingDMChannel(user1, user2)
    if existingChannel != "" (
        return existingChannel
    )

    string channelId = randomString(10)
    object channel = {
        id: channelId,
        type: "dm",
        name: channelId,
        members: [user1, user2],
        created_at: timestamp / 1000
    }
    saveChannel(channelId, channel)
    addChannelToUser(user1, channelId)
    addChannelToUser(user2, channelId)
    return channelId
)

def createGroupChannel(string creator, string name, array members) string (
    string channelId = randomString(10)
    array allMembers = [creator]
    array memberIds = []
    int len = members.len
    int i = 1
    while i <= len (
        string username = members[i].toStr()
        string memberId = getIdByUsername(username)
        if memberId != "" (
            allMembers.append(memberId)
            memberIds.append(memberId)
            addChannelToUser(memberId, channelId)
        )
        i = i + 1
    )
    addChannelToUser(creator, channelId)
    
    object channel = {
        id: channelId,
        type: "group",
        name: name,
        display_name: channelId,
        members: allMembers,
        creator: creator,
        created_at: timestamp / 1000
    }
    saveChannel(channelId, channel)
    return channelId
)

def addUserToGroupChannel(string channelId, string id) bool (
    object ch = getChannel(channelId)
    if ch == null (
        return false
    )
    if ch.type.toStr() != "group" (
        return false
    )
    
    if typeof(ch.members) != "array" (
        ch.members = []
    )
    
    array members = ch.members.assert(array)
    if members.contains(id) (
        return false
    )
    
    members.append(id)
    ch.members = members
    saveChannel(channelId, ch)
    addChannelToUser(id, channelId)
    return true
)

def sendChannelMessage(string channelId, string content, string id) object (
    object ch = getChannel(channelId)
    if ch == null (
        return null
    )

    if typeof(ch.members) != "array" (
        return null
    )

    array members = ch.members.assert(array)
    if !members.contains(id) (
        log "User not in channel"
        return null
    )

    if typeof(ch.messages) != "array" (
        ch.messages = []
    )

    string messageId = randomString(10)
    object message = {
        id: messageId,
        user: id,
        content: content,
        timestamp: timestamp / 1000,
        type: "message"
    }

    array messages = ch.messages.assert(array)
    messages.append(message)
    ch.messages = messages
    saveChannel(channelId, ch)
    return message
)

def getChannelMessages(string channelId) array (
    object ch = getChannel(channelId)
    if ch == null (
        return []
    )

    if typeof(ch.messages) != "array" (
        return []
    )

    return ch.messages.assert(array)
)

def editChannelMessage(string channelId, string messageId, string newContent, string id) bool (
    object ch = getChannel(channelId)
    if ch == null (
        return false
    )

    if typeof(ch.messages) != "array" (
        return false
    )

    array messages = ch.messages.assert(array)
    for i messages.len (
        auto msg = messages[i]
        if typeof(msg) == "object" (
            object message = msg.assert(object)
            if message.id.toStr() == messageId (
                if message.user.toStr() != id (
                    return false
                )
                message.content = newContent
                message.edited = true
                message.edited_at = timestamp / 1000
                messages[i] = message
                ch.messages = messages
                saveChannel(channelId, ch)
                return true
            )
        )
    )
    return false
)

def deleteChannelMessage(string channelId, string messageId, string id) bool (
    object ch = getChannel(channelId)
    if ch == null (
        return false
    )

    if typeof(ch.messages) != "array" (
        return false
    )

    array messages = ch.messages.assert(array)
    array newMessages = []
    bool deleted = false
    for i messages.len (
        auto msg = messages[i]
        if typeof(msg) == "object" (
            object message = msg.assert(object)
            if !deleted and message.id.toStr() == messageId (
                if message.user.toStr() != id (
                    return false
                )
                deleted = true
            ) else (
                newMessages.append(messages[i])
            )
        ) else (
            newMessages.append(messages[i])
        )
    )
    if deleted (
        ch.messages = newMessages
        saveChannel(channelId, ch)
        return true
    )
    return false
)

def getMessage(string channelId, string messageId) object (
    object ch = getChannel(channelId)
    if ch == null (
        return null
    )

    if typeof(ch.messages) != "array" (
        return null
    )

    array messages = ch.messages.assert(array)
    for i messages.len (
        auto msg = messages[i]
        if typeof(msg) == "object" (
            object message = msg.assert(object)
            if message.id.toStr() == messageId (
                return message
            )
        )
    )
    return null
)
